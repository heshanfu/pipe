buildscript {
    ext {
        versions = [
                'junit' : '4.12',
                'kotlin': [
                        'coroutines': '0.22.5',
                        'lang': '1.2.41'
                ]
        ]

        coreVersions = [
                'pipe': '0.0.1-alpha1'
        ]

        depends = [
                'junit' : "junit:junit:${versions.junit}",
                'kotlin': [
                        'coroutines'   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
                        'gradle_plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.lang}",
                        'stdlib'       : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin.lang}"
                ]
        ]
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath depends.kotlin.gradle_plugin
    }
}

group 'com.udeyrishi'
version coreVersions.pipe

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation depends.kotlin.stdlib
    implementation depends.kotlin.coroutines
    testImplementation depends.junit
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.allWarningsAsErrors = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.allWarningsAsErrors = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'pipe'
            version = coreVersions.pipe
            from components.java
        }
    }
}