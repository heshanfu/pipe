buildscript {
    ext {
        versions = [
                'android': [
                    'gradle_plugin': '3.1.3',
                    'lifecycle': [
                            'common_java8': "1.1.1"
                    ],
                    'support': [
                            'appcompat7': '27.1.1',
                            'espresso': '3.0.2',
                            'test_runner': '1.0.2'
                    ]
                ],
                'junit' : '4.12',
                'kotlin': [
                        'coroutines': '0.22.5',
                        'lang': '1.2.41'
                ],
                'mockito': '2.19.1'
        ]

        coreVersions = [
                'buildTools': '27.0.3',
                'targetSdkVersion': 27,
                'minSdkVersion': 15,
                'compileSdkVersion': 27,
                'versionCode': 1,
                'versionName': '0.0.1-alpha1'
        ]

        depends = [
                'android': [
                    'gradle_plugin': "com.android.tools.build:gradle:${versions.android.gradle_plugin}",
                    'lifecycle': [
                        'common_java8': "android.arch.lifecycle:common-java8:${versions.android.lifecycle.common_java8}"
                    ],
                    'support': [
                            'appcompat7': "com.android.support:appcompat-v7:${versions.android.support.appcompat7}",
                            'espresso': "com.android.support.test.espresso:espresso-core:${versions.android.support.espresso}",
                            'test_runner': "com.android.support.test:runner:${versions.android.support.test_runner}"
                    ]
                ],
                'junit' : "junit:junit:${versions.junit}",
                'kotlin': [
                        'coroutines'   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
                        'gradle_plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.lang}",
                        'stdlib'       : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin.lang}"
                ],
                'mockito': [
                        'core': "org.mockito:mockito-core:${versions.mockito}"
                ]
        ]
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath depends.kotlin.gradle_plugin
        classpath depends.android.gradle_plugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.allWarningsAsErrors = true
}